# This class is not explicitly included by the user; instead it is included
# by the `fossa.bbclass` adjacent to this class.

inherit fossa_utils

addtask do_fossa_analyze before do_build after do_rootfs
do_fossa_analyze[doc] = "Analyze via fossa-cli"
do_fossa_analyze[nostamp] = "1"
do_fossa_analyze[depends] = "fossa-cli:do_populate_sysroot"

addtask do_fossa_test before do_build after do_fossa_analyze
do_fossa_test[doc] = "Test via fossa-cli"
do_fossa_test[nostamp] = "1"
do_fossa_test[deptask] += "fossa-cli:do_populate_sysroot"

# This task runs `fossa-cli` against the `fossa-deps` file generated by `fossa:do_fossa`,
# analyzing the file and storing its results in the FOSSA backend.
# 
# This task is run after `do_rootfs` is finalized (`fossa:do_fossa` runs as a post-processing
# step of the `do_rootfs` task, so is run immediately before this task runs).
python do_fossa_analyze() {
    if not is_fossa_enabled(d):
        bb.debug(1, "Since FOSSA_ENABLED is 0, skipping: fossa analyze")
        return

    staging_dir = d.getVar("FOSSA_STAGING_DIR") 
    if not staging_dir:
        bb.fatal("expected to have value for FOSSA_STAGING_DIR, but recieved Nothing")

    run_fossa_cli(d, mk_fossa_cmd(d, 'analyze'))
}

# This task runs the `test` subcommand of `fossa-cli`, ensuring that the project
# has no issues before the image build proceeds.
#
# This task is run after `do_fossa_analyze`, and is the last task run before
# the actual image build task.
python do_fossa_test() {
    if not is_fossa_enabled(d):
        bb.debug(1, "Since FOSSA_ENABLED is 0, skipping: fossa test")
        return 

    if is_fossa_analyze_only(d):
        bb.debug(1, "Since FOSSA_ANALYZE_ONLY is 1, skipping: fossa test")
        return

    run_fossa_cli(d, mk_fossa_cmd(d, 'test'))
}

def run_fossa_cli(d, cli_args):
    import os
    import subprocess

    BINDIR = d.getVar("bindir")
    WORKDIR = d.getVar("WORKDIR")

    cli_path = (f"{WORKDIR}/recipe-sysroot{BINDIR}/fossa")
    cmds = [cli_path] + cli_args
    bb.plain(f"running: {' '.join(cmds)}")

    out = subprocess.run(cmds, cwd=d.getVar("FOSSA_STAGING_DIR"), capture_output=True, text=True, shell=False)
    if out.returncode != 0:
        bb.fatal(out.stderr)
    else:
        # fossa analyze summary is printed in stderr
        bb.plain(out.stderr)
        bb.plain(out.stdout)
